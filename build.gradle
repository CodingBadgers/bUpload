buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "ForgeFS"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
            name 'Shadow'
            url 'http://dl.bintray.com/content/johnrengelman/gradle-plugins'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
		classpath 'org.gradle.plugins:shadow:0.7.4'
    }
}

apply plugin: "forge"
apply plugin: 'shadow'

// define the properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // read config. it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.3.1'
	compile 'commons-net:commons-net:3.3'
}

group = "uk.thecodingbadgers"
version = config.mod_version
archivesBaseName = "bUpload"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version // grab latest forge
    assetDir = "run/assets"
}

// add some stuff to the version
version = "${config.minecraft_version}-${config.mod_version}"

processResources () {
    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replaces
        expand ([
            'mod_version': project.config.mod_version,
            'minecraft_version': project.config.minecraft_version,
			'mod_url': "http://www.minecraftforum.net/topic/1432205-16x-bupload-upload-screenshots-straight-to-imgurcom/"
        ])
    }

    // copy everything else, thats we didnt do before
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

artifacts {
    archives sourceJar
    archives deobfJar
}