buildscript {
    repositories {
        mavenCentral()
		mavenLocal()
        jcenter()
		
        maven {
            name = "ForgeFS"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
    }
}

apply plugin: "forge"
apply plugin: "shadow"

dependencies {
	compile 'org.apache.httpcomponents:httpclient:4.3.1'
	compile 'commons-net:commons-net:3.3'
	compile 'org.twitter4j:twitter4j-core:3.0.5'
}

ext {
	modVersion="1.4.1-SNAPSHOT"
	url = "http://tinyurl.com/bUpload-MF"
}

group = "uk.thecodingbadgers"
version = ext.modVersion
archivesBaseName = "bUpload"

minecraft {
    version = "1.7.2-10.12.1.1091"
	mcVersion = "1.7.2"
    assetDir = "eclipse/assets"
}

// add some stuff to the version
version = "${project.minecraft.mcVersion}-${project.version}"

processResources () {
    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replaces
        expand ([
            'mod_version': project.ext.modVersion,
            'minecraft_version': project.minecraft.version,
			'mod_url': project.ext.url
        ])
    }

    // copy everything else, thats we didnt do before
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

shadow {
    destinationDir "${buildDir}/libs/"
	
    artifactSet {
		include 'org.apache.httpcomponents:*:*'
		include 'commons-net:commons-net:*'
		include 'commons-codec:commons-codec:*'
		include 'commons-logging:commons-logging*'
		include 'org.twitter4j:twitter4j-core:*'
    }
	
    relocation {
        pattern = 'org.apache.commons.codec'
        shadedPattern = 'uk.codingbadgers.libs.org.apache.commons.codec'
    }
	
    relocation {
        pattern = 'org.apache.commons.logging'
        shadedPattern = 'uk.codingbadgers.libs.org.apache.commons.logging'
    }
	
    relocation {
        pattern = 'org.apache.commons.net'
        shadedPattern = 'uk.codingbadgers.libs.org.apache.commons.net'
    }
	
    relocation {
        pattern = 'org.apache.http'
        shadedPattern = 'uk.codingbadgers.libs.org.apache.http'
    }
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

artifacts {
    archives sourceJar
    archives deobfJar
}

shadowJar.dependsOn(reobf)
build.dependsOn(shadowJar)